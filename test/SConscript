from os import environ, path

Import('coverage')
Import('env')
Import('rose_lib')

env = env.Clone()

GTEST_DIR = '#external/googletest'

env.Prepend(LIBS=[rose_lib],
            CPPPATH=[Dir(GTEST_DIR),
                     Dir(GTEST_DIR + '/include')])

test = env.Program(target='test',
                   source=[env.Glob('*.cpp'),
                           File(GTEST_DIR + '/src/gtest-all.cc')])

run_source = [test[0]]

if coverage:
    lcov_reset = env.LCov(target='lcov-reset',
                          source=[test],
                          LCOVRESET=True,
                          LCOVDIR='.',
                          LCOVBASEDIR='#/')

    run_source = [test[0], lcov_reset]

run = env.Command(target='unit-test',
                  source=run_source,
                  action=test[0].abspath)

if coverage:
    env.GenHtml(target=Dir('html'),
                source=env.LCov(target='coverage.info',
                                source=[test, run, lcov_reset],
                                LCOVDIR='.',
                                LCOVBASEDIR='#/'))

    env.Clean(test,
              [env.Dir('html'),
               env.File('test.gcno')])

# vim:ft=python ts=4 sw=4 sts=4 et
