Import('env')

env = env.Clone()
env.Append(LIBS='gc',
           CPPDEFINES='QUEX_OPTION_ASSERTS_WARNING_MESSAGE_DISABLED',
           YACCFLAGS='-d')

lex_src = env.Quex(source='lexer.qx',
                   QUEXENGINE='lexer',
                   QUEXLANG='C',
                   QUEXOUTDIR=Dir('quex').abspath,
                   QUEXTKNID=File('detail/token-ids.h').abspath,
                   QUEXFLAGS=['--token-id-prefix TKN_',
                              '--token-policy queue',
                              '--token-class token'])

parser_src = env.CFile(source='parser.y')
rsi_obj = env.Object(source=[Glob('*.c'), Glob('quex/*.c')])
rsi_bin = env.Program(target='rsi', source=rsi_obj)

env.Depends(target=rsi_obj, dependency=lex_src)
env.Depends(target=rsi_obj, dependency=parser_src)
env.Depends(target=lex_src, dependency=parser_src)

Return('rsi_bin')

# vim:ft=python
